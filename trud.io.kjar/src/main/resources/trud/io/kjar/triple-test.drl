package trud.io.kjar;
import com.sbnz.trud.io.model.Pregnancy;
import com.sbnz.trud.io.model.TripleTest;
import com.sbnz.trud.io.model.Amniocentesis;
import com.sbnz.trud.io.model.AgeRisk;
import com.sbnz.trud.io.model.WeeklyParameters;
import java.util.Collection;
import java.time.LocalDate;
import java.lang.Math;
import java.time.temporal.ChronoUnit;
import java.time.Period;

rule "Triple test - trisomy risks" salience 10000
	no-loop
	agenda-group "tripleTest"
    when
     	$pregnancy: Pregnancy(birth == null, tripleTest != null)
        $week: Number() from $pregnancy.getWeek()
        $t: TripleTest(result == 'Not yet processed') from $pregnancy.getTripleTest()
        $age: Number() from $pregnancy.getPatient().checkAge()
       	$riskAgeT21: AgeRisk(age == $age, trisomy == 'Trisomy21' )
       	$riskAgeT18: AgeRisk(age == $age, trisomy == 'Trisomy18' )
       	$riskAgeT13: AgeRisk(age == $age, trisomy == 'Trisomy13' )
       	
        $weeklyParameters: WeeklyParameters(week == $week)
        
      	$coefAFP: Double() from $t.getCoefAfp($weeklyParameters.getMedianAfp())
      	$coefUe3: Double() from $t.getCoefUe3($weeklyParameters.getMedianUe3())
        $coefHCG: Double() from $t.getCoefHCG($weeklyParameters.getMedianHcg())
        $coefSmoker: Double() from $t.getCoefSmoker($pregnancy.getPatient().isSmoker())
        
        $risk : Double() from 1.053 * 1.24 * 2.03 *  $coefAFP * $coefUe3 * $coefHCG * $coefSmoker
        $riskT21: Double() from $riskAgeT21.getRisk() * $risk
        $riskT18: Double() from $riskAgeT18.getRisk() * $risk
        $riskT13: Double() from $riskAgeT13.getRisk() * $risk
        
    then
        modify($t) {
        	setTrisomy21($riskT21),
        	setTrisomy18($riskT18),
        	setTrisomy13($riskT13),
        	setResult('Calculate risks');
        }
end

rule "Triple test - positive, in family genetic anomalies" salience -1000
	agenda-group "tripleTest"
    when
     	$pregnancy: Pregnancy(birth == null, tripleTest != null, amniocentesis == null, highRiskPregnancy == false)
     	$patient: Patient(geneticAnomalies == true)  from $pregnancy.getPatient()
     	$t: TripleTest( resultT21 == 'Screen positive' || resultT18 == 'Screen positive' || resultT13 == 'Screen positive', status == 'Positive - not Amniocentesis') from $pregnancy.getTripleTest()
     	
    then
    	Amniocentesis a = new Amniocentesis();
       	insert(a);
       	modify($t){
        	setStatus('Positive - Amniocentesis');
        }
       
end

rule "Triple test - positive" salience -1000
	agenda-group "tripleTest"
    when
     	$pregnancy: Pregnancy(birth == null, tripleTest != null, amniocentesis == null)
     	$t: TripleTest( resultT21 == 'Screen positive' || resultT18 == 'Screen positive' || resultT13 == 'Screen positive', status == 'Positive - not Amniocentesis') from $pregnancy.getTripleTest()
     	
    then
    	modify($t){
        	setStatus('Positive');
        }
end

